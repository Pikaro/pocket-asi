root ::= actionlist

actionlist ::= "[" ws action ws ("," ws action)* ws "]"

action ::= (
    "{" ws
        (command | file)
        ("," ws comment)?
    "}"
)

command ::= "\"command\"" ws ":" string

file ::= (
        "\"file\"" ws ":" string
        ("," ws content)?
)

content ::= "\"content\":" string
comment ::= "\"comment\":" string

# Too permissive - models start printing Chinese / Cyrillic
# Not sure if they actually use nonprintable
# string ::= (
#     ws "\"" (
#         [^"\\\x7F\x00-\x1F] |
#         "\\" (["\\/bfnrt] | "u" [0-9a-fA-F] [0-9a-fA-F] [0-9a-fA-F] [0-9a-fA-F])
#     )* "\"" ws
# )

# This allows all of ASCII printable except unescaped " and \
# ` is prohibited entirely so the models don't use Markdown
string ::= (
    ws "\"" (
        [ !] | [#-\[] | [\]-_] | [a-~] | "\\\"" | "\\\\"
    )* "\"" ws
)

ws ::= [ \t\n]*
